plugins {
    id 'java'
    id 'war'
    id 'com.github.node-gradle.node' version '3.0.1'
    id 'org.springframework.boot' version '3.0.1'
    id 'io.spring.dependency-management' version '1.1.0'
}

group = 'com.timothy20'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

node {
    download = true
    version="14.17.6"
    npmVersion="6.14.15"
    distBaseUrl="https://nodejs.org/dist"
    npmInstallCommand="install"
    workDir=file("${project.projectDir}/.gradle/nodejs")
    npmWorkDir=file("${project.projectDir}/.gradle/npm")
    nodeProjectDir=file("${project.projectDir}/frontend")
    nodeProxySettings=ProxySettings.SMART
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    // springboot 기본 설정
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-devtools'

    // jpa 로그
    implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.8.1'

    implementation 'junit:junit:4.13.2'
    runtimeOnly 'com.h2database:h2'

    // database
    implementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '3.1.0'
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.1'
//    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

apply plugin: "com.github.node-gradle.node"

task deleteVueBuildFiles(type: Delete) {
    delete "src/main/resources/static/vue", "src/main/resources/static/template/vue/index.html", "${project.projectDir}/frontend/node_modules"
}

task npmBuild(type: NpmTask, dependsOn: ['deleteVueBuildFiles', 'npmInstall']) {
    args=["run", "build"]
}

processResources.dependsOn 'npmBuild'

tasks.named('test') {
    useJUnitPlatform()
}
